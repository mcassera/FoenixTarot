' Foenix Tarot
' 2024, Michael Cassera
'
' set some screen and MMU parameters
poke 1,0
pokel $d00d,$000000
cursor off

' load the ML routine for memory copy
bload "tarot/mlcopy2$7800.bin",$7800

' turn on the bitmap and clear
bitmap on
bitmap clear 0
cls

' load font and palette info
loadfont()
loadpal()

' load the title screen live
bload "tarot/tarot_title.bin",$10000

' load and display the welcome text 
bload "tarot/welcome.txt",$6000
printtext(0,0)

' load some assets - backs of cards etc.
bload "tarot/tarot_3Card.bin",$30000
bload "tarot/f79.bin",$40000
bload "tarot/r79.bin",$42bc0

' Set some arrays
dim deck$(78),spread$(11),pos(3),cmem(3)
pos(1)=22:pos(2)=120:pos(3)=218
cmem(1)=$45780:cmem(2)=$48340:cmem(3)=$4af00
cards=alloc(78)
user=alloc(20)

' Get the 4 types of spreads we can do
for n=0 to 11
  read spread$(n)
next

' load user data if there is any
bload "tarot/user.bin",user

' Main routine for the program
repeat
  clearspread()
  user$=""
  
' Get the user name and spread from previous session  
  for n=0 to 9
    if peek(user+n)<>asc(".") then user$=user$+chr$(peek(user+n))
  next
  usersize=len(user$)
  sp=val(chr$(peek(user+10)))

  if user$="new" 
    bload "tarot/newuser.txt",$6000
    printtext(0,0)
    newuser=1
  else
    bload "tarot/olduser.txt",$6000
    printtext(usersize,user)
    repeat
    until peek($680)<>0
    poke $680,0
    k=peek($678)
    if (k=89) | (k=121)
      msg=peek(user+10)
      newuser = 0
    else
      newuser = 2
    endif
  endif
  
  if newuser=2
    bload"tarot/changeuser.txt",$6000
    printtext(usersize,user)
    newuser=1
  endif
  
  ' Show old spread
  if newuser=0
    bload "tarot/viewoldspread.txt",$6000
    printtext(usersize,user)
    repeat
      repeat
      until peek($680)<>0
      keypress=peek($678):poke $680,0
    until (keypress=asc("y")) | (keypress=asc("n"))
    if keypress=asc("y")
      oldcards(user, sp)
      viewdes(1)
      clearspread()
      cls
    endif  
  endif
    
  ' enter new user name here
  if newuser=1
    ix=35:iy=57
    inputpos=$c000+(iy*80)+ix
    poke 1,2
    for n=0 to 9
      poke inputpos+n,46
      poke user+n,46
    next
  

    keypress=0:n=0
    while keypress<>13
      repeat
      until peek($680)<>0
      keypress=peek($678):poke $680,0
      if keypress=8
        n=n-1
        if n<0 then n=0      
        poke inputpos+n,46
        poke user+n,46
      endif
      if ((keypress>64)&(keypress<91))|((keypress>96)&(keypress<123))
        poke inputpos+n,keypress
        poke user+n,keypress
        n=n+1:if n>9 then n=9
      endif
    wend
    poke 1,0    
    user$=""
    for n=0 to 9
      if peek(user+n)<>asc(".") then user$=user$+chr$(peek(user+n))
    next
    if user$="" 
      user$="BOB"
      poke user,66
      poke user+1,79
      poke user+2,66
    endif
    usersize=len(user$)
  endif
  
  ' start the reading by selecting type of spread
  clearspread()  
  if newuser=0 then bload"tarot/welcomeold.txt",$6000
  if newuser=1 then bload"tarot/welcomenew.txt",$6000
  printtext(usersize,user)
  
  cursorpos(59,20)
  print chr$(128+7);"LEFT/RIGHT to navigate, RETURN when done";
  
  keypress=0:sp=0
  print chr$(12+128);
  while keypress<>13
    cursorpos(43,9)
    print spread$(0+sp)
    cursorpos(43,33)
    print spread$(1+sp)
    cursorpos(43,57)
    print spread$(2+sp)
    repeat
    until peek($680)<>0
    keypress=peek($678):poke $680,0
    if keypress=6
      sp=sp+3
      if sp>9 then sp=0
    endif
    if keypress=2
      sp=sp-3
      if sp<0 then sp=9
    endif
  wend
  
  
  cursorpos(59,20)
  print chr$(128+7);"                                        ";
  
  
  ' Now tell the user the think of a question before the draw
  ' The computer is constantly picking random numbers
  ' while waiting - to make it more random.
  if newuser=0 then bload"tarot/questionold.txt",$6000
  if newuser=1 then bload"tarot/questionnew.txt",$6000
  printtext(usersize,user)
  spacebar()
  call $78f2
  
  ' clear the cards and shuffle the deck    
  clearcards(cards)
  shufflecards(cards)
  drawcards()
  
  ' weird reset of name in memory - required to keep the name 
  ' from corruption
  for n=0 to usersize
    poke user+n,asc(mid$(user$,n+1,1))
  next  
  
  if newuser=0 then bload"tarot/readytoviewold.txt",$6000
  if newuser=1 then bload"tarot/readytoviewnew.txt",$6000
  
  printtext(usersize,user)
  spacebar()



  ' reveal cards
  for p=1 to 3
    card$="tarot/"+deck$(p)
    loadtxt(card$)
    displaycard(cmem(p),$10000+(320*23)+pos(p),80,139)
    if p<3
      printtext(0,0)
      spacebar()
    endif     
  next
  
  viewdes(3)



bload"tarot/another.txt",$6000
printtext(usersize,user)

' save user data
us$=user$+left$("..........",10-len(user$))
us$=us$+str$(sp)+deck$(1)+deck$(2)+deck$(3)
for g=0 to 19
  poke user+g,asc(mid$(us$,g+1,1))
next
bsave"tarot/user.bin",user,20

cursorpos(59,18)
print chr$(128+7);"'Q' to quit, anything else for new reading";  

repeat
until peek($680)<>0
keypress=peek($678):poke $680,0

cls
until keypress=asc("q")

bitmap off:poke $d000,1
cursor on
print chr$(128+9)
print "    Thanks for using Foenix Tarot, goodbye ";user$;"."
print
end

' Wait for Spacebar
proc spacebar()
  cursorpos(59,28)
  print chr$(128+7);"Press space to continue";
  repeat
    repeat
    q=random(78)
    until peek($680)<>0
    poke $680,0
  until peek($678)=32
  cursorpos(59,28)
  print "                        ";
  cursorpos(0,0)
endproc

' Load the color palette
proc loadpal()
  bload "tarot/tarot.pal",$4000
  poke 1,1
  for n=0 to 256*4    
    poke $D000+n,peek($4000+n)
  next
  poke 1,0
endproc

' load image
proc loadpic(filename$,loc)
  picname$=filename$+".bin"
  bload picname$,loc
endproc

' load text
proc loadtxt(filename$)
  textname$=filename$+".txt"
  bload textname$,$6000
endproc

' clear the deck to shuffle
proc clearcards(cards)
  for n=0 to 78
    poke cards+n,0
  next
endproc

' clear the screen display of cards
proc clearspread()
  displaycard($30000,$10000,320,167)
endproc

' draw the cards for the spread
proc drawcards()  
  ' draw cards and display backs
  for n=1 to 3
    card$="tarot/"+deck$(n)
    loadpic(card$,cmem(n))
    if left$(deck$(n),1)="f"
      displaycard($40000,$10000+(320*23)+pos(n),80,139) 
     else
      displaycard($42bc0,$10000+(320*23)+pos(n),80,139)
    endif
  next
endproc

' display a previous spread
proc oldcards(user,sp)  
  ' draw cards and display
  deck$(1)=chr$(peek(user+11))+chr$(peek(user+12))+chr$(peek(user+13))
  deck$(2)=chr$(peek(user+14))+chr$(peek(user+15))+chr$(peek(user+16))
  deck$(3)=chr$(peek(user+17))+chr$(peek(user+18))+chr$(peek(user+19))

  print chr$(128+4);
  cursorpos(43,9)
  print spread$(0+sp)
  cursorpos(43,33)
  print spread$(1+sp)
  cursorpos(43,57)
  print spread$(2+sp)    
        
  for n=1 to 3
    card$="tarot/"+deck$(n)
    loadpic(card$,cmem(n))
    displaycard(cmem(n),$10000+(320*23)+pos(n),80,139) 
  next
endproc

' display a card using ml routine and DMA
proc displaycard(s,d,sx,sy)
  pokel $7803,s
  pokel $7806,d
  pokel $7809,sx
  poke $780C,sy
  call $7800
endproc

' load the font
proc loadfont()
  bload "tarot/tarot_letters2.bin",$4000
  poke 1,1
  for n=32*8 to 123*8
    poke $C000+n,peek($4000+n-256)
  next
  poke 1,0
endproc

' Shuffle the card for the draw
proc shufflecards(cards)
  for n=1 to 78
    repeat
      c=random(78)+1
    until peek(cards+c)=0
    poke cards+c,1
    o=random(2)+1
    deck$(n)=mid$("fr",o,1)
    if c<10 then deck$(n)=deck$(n)+"0"
    deck$(n)=deck$(n)+str$(c)
  next
endproc

' print the block text at the lower portion of the screen
proc printtext(usersize,user)
  call $78f2
  j=0:y=0:n=0
  ul=0
  repeat
    a=peek($6000+n)
    
    if a=37
      for k=0 to usersize-1
        a=peek(user+k)
        poke 1,2
        poke ($C00C+(48*80))+(y*80)+j,a
        poke 1,3
        poke ($C00C+(48*80))+(y*80)+j,$E2
        poke 1,0
        j=j+1
      next
      n=n+1
      a=peek($6000+n)
    endif  
    
    if (a<>94)&(a<>13)
      poke 1,2
      poke ($C00C+(48*80))+(y*80)+j,a
      poke 1,3
      poke ($C00C+(48*80))+(y*80)+j,$E2
      poke 1,0
      n=n+1:j=j+1
    endif
    if a=(13)&(y=0)
      j=0
      y=y+2:n=n+2
    endif
    if (j>50)&(a=32)
      j=0
      y=y+1
    endif
  until a=94
endproc

' set the cursor position for printing
proc cursorpos(y,x)
  for n=0 to 60
    print chr$(16);
  next
  print chr$(1);
  for n=1 to y
    print chr$(14);
  next
  for n=1 to x
    print chr$(6);
  next
endproc

' view the descriptions of the cards
proc viewdes(cd)
  ' press through descriptions
 
  cursorpos(59,20)
  print chr$(128+7);"LEFT/RIGHT to navigate, RETURN when done";
  repeat

    
    if cd=1 
      cursorpos(43,9):print chr$(128+15);spread$(sp)
      cursorpos(43,33):print chr$(128+4);spread$(sp+1)
      cursorpos(43,57):print chr$(128+4);spread$(sp+2)
    endif
    if cd=2
      cursorpos(43,9):print chr$(128+4);spread$(sp)
      cursorpos(43,33):print chr$(128+15);spread$(sp+1)
      cursorpos(43,57):print chr$(128+4);spread$(sp+2)
    endif
    if cd=3
      cursorpos(43,9):print chr$(128+4);spread$(sp)
      cursorpos(43,33):print chr$(128+4);spread$(sp+1)
      cursorpos(43,57):print chr$(128+15);spread$(sp+2)
    endif
  
    card$="tarot/"+deck$(cd)
    loadtxt(card$)
    printtext(0,0)
    
    repeat
    until peek($680)<>0
    keypress=peek($678):poke $680,0
    if keypress=6
      cd=cd+1:if cd>3 then cd=1
    endif
    if keypress=2
      cd=cd-1:if cd<1 then cd=3
    endif
    
  until keypress=13

  cursorpos(59,20)
  print chr$(128+7);"                                        ";
endproc


' data for the spreads
data "     Past    ","   Present   ","    Future   "
data "  Situation  ","   Problem   ","    Action   "
data "     You     ","     Me      ","      Us     "
data "  Option A   ","   Option B  ","How to decide"
